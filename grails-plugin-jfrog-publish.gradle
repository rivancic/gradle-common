// region Release configuration
buildscript {
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

jar {
    exclude "com/wizpanda/UrlMappings**"
}

publishing {
    publications {
        wpJFrogMaven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "${pomName}".toString()
                description = "${pomDescription}".toString()
                url = "${pomURL}".toString()
                developers {
                    developer {
                        id = 'sagrawal31'
                        name = 'Shashank Agrawal'
                        email = 'creative@wizpanda.com'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/${pomGitHubSlug}.git".toString()
                    developerConnection = "scm:git:ssh://github.com:wizpanda/kernel.git".toString()
                    url = "https://github.com/${pomGitHubSlug}/tree/main".toString()
                }
            }
        }
    }
}

artifactory {
    contextUrl = "https://wizpanda.jfrog.io/artifactory"
    publish {
        repository {
            repoKey = "default-maven-local"
            username = "${wpArtifactoryUsername}".toString()
            password = "${wpArtifactoryPassword}".toString()
        }
        defaults {
            publications('wpJFrogMaven')
            publishArtifacts = true
        }
    }
}

// endregion
